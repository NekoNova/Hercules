//========================================================================
// The NPCs defined in this file will open up quests at specific levels,
// that are triggered by Elys on the correct level. These quests can be
// done once by everyone who meets the level requirement, regardless of
// his class.
//========================================================================
prt_in,85,106,3	script	Distressed Captain	628,{
	if(checkquest(30000) == 1){
		emotion 19,0;
		mes "[Distressed Captain]";
		mes "Oh...I don't know what to do...";
		mes "Hey, you there! Yes you!";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "me?";
		next;
		mes "[Distressed Captain]";
		mes "Yes, you adventurer, pleas help me out";
		mes "A company of Knights has ventured into the ^5EAE9EEHidden Temple^000000";
		mes "But they have not returned yet. Please do me a favour and go look for them.";
		close2;
		changequest 30000,30001;
		end;
	} else if(checkquest(30002) == 1) {
		mes "[Distressed Captain]";
		mes "They are okay? Thank you adventurer.";
		mes "That's the best news I've heard all day.";
		mes "I'll be sure to notify my superiors to keep an eye on you.";
		mes "We could always use more able adventurers.";
		next;
		mes "[Distressed Captain]";
		mes "Perhaps in the future we might call upon your servics again.";
		mes "Until then, have a safe journey.";
		close2;
		completequest 30002;
		getexp 200000,30000;
		end;
	} else if(checkquest(30002) == 2) {
		mes "[Distressed Captain]";
		mes "Many thanks again adventurer for helping us locate the missing knights.";
		mes "One of them even requested more training.";
		mes "It seems you inspired them.";
		close;
	} else {
		emotion 23,0;
		mes "[Distressed Captain]";
		mes "What are you still standing here for!?";
		mes "They could be severaly injured or worse!";
		mes "Please go look for them in the ^EAE9EEHidden Temple^000000";
		close;
	}
}

prt_fild01,142,364,4	script	Wounded Knight	686,{
	emotion 19,0;
	
	if(checkquest(30001) == 1){
		mes "[Wounded Knight]";
		mes "Oh thank you for coming to our aid.";
		mes "We got ambushed by the Demon Baphomet and....";
		mes "Look Out!!!";
		close2;
		monster "this",142,358,"Baphomet Jr.",1729,2,strnpcinfo(0)+"::OnMonsterDead";
		end;
	} else if(checkquest(30002) == 1 || checkquest(30002) == 2){
		mes "[Wounded Knight]";
		mes "Wow, your combat skills are amazing, adventurer.";
		mes "These monsters forced us to retreat from the Hidden Temple";
		mes "Please return to the Guild and let our Captain know that we're fine";
		close;
	} else {
		mes "[Wounded Knight]";
		mes "Oh god....The monsters....we......";
		mes "^0000FFIt seems he's too much in a shock to notice you.^000000";
		close;
	}
	
OnMonsterDead:
	changequest 30001,30002;
end;
}

prt_fild01,143,362,4	script	Scared Knight	500,{
	if(checkquest(30002) == 1 || checkquest(30002) == 2){
		mes "[Scared Knight]";
		mes "Wow, your combat skills are amazing, adventurer,";
		mes "I'm going to ask the Captain to train us harder.";
		mes "That should prepare us better for future campaigns.";
		close;
	} else {
		emotion 79,0;
		mes "[Scared Knight]";
		mes "^0000FFThe knight if frozen with fear and does not seem to notice you.^000000";
		close;
	}
}