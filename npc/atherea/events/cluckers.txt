//===== Hercules Script =======================================
//= Cluck! Cluck! Boom!
//===== By: ==================================================
//= Keale
//===== Current Version: =====================================
//= 1.2a
//===== Description: =========================================
//= Click the chicken and try retrieve the item at a low 
//= chance. If you fail he will nuke, freeze, stone, 
//= stun, or make you fall asleep.
//= The prize is configurable and triggered by the NPC.
//===== Additional Comments: =================================
//= 1.0 First release.
//= 1.1 Using 'switch rand' instead.
//= 1.2 Cleaned and standardized. [Euphy]
//= 1.2a Switched 'atcommand' to 'unitskilluseid'.
//============================================================

prontera,155,166,4	script	Cluckers	4_NFCOCK,{

	set .@GMAccess,60;	// GM level required to access NPC

	if (.startcluck) {
		specialeffect2 2;
		switch(rand(15)) {
			case 0: npctalk "CLUUUUUUCK!!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 1: npctalk "Cluuuuuck!~"; break;
			case 2: unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; break;
			case 3: sc_start SC_FREEZE,10000,0; break;
			case 4: npctalk "CLUUUUUUUUUCK!!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 5: sc_start SC_SLEEP,10000,0; break;
			case 6: sc_start SC_STONE,10000,0; emotion 29; break;
			case 7: npctalk "CLUUUUUUCK!!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 8: npctalk "Cluck! CLUUUCK!!"; unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; emotion 23; break;
			case 9: sc_start SC_STUN,10000,0; break;
			case 10: sc_start SC_SLEEP,10000,0; emotion 29; break;
			case 11: npctalk "Cluck! Cluck!"; break;
			case 12: sc_start SC_STUN,10000,0; break;
			case 13: unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1; break;
			default:
				if (rand(50) < 4) {
					npctalk "WOOF!...........";
					specialeffect2 72;
					announce "[Cluck! Cluck! Boom!] " + strcharinfo(0) + " squeezed out the prize! Well done!",0;
					getitem $cluck_item_id,$cluck_item_amount;
					set .startcluck,0;
				} else {
					npctalk "Cluck! CLUUUCK!!";
					unitskilluseid getcharid(3),"NPC_SELFDESTRUCTION",1;
				}
				break;
		}
		end;
	}
	mes "[Cluckers]";
	if (getgmlevel() >= .@GMAccess) {
		mes "Cluck cluck! Cluuuuuck? ^FF0000~Hi GM " + strcharinfo(0) + ", Wanna play today?~^000000";
		mes "Cluck cluck... CLUCK! ^FF0000~Just tell me what to do!~^000000";
		next;
		while(1) switch(select("Start Event:Check Prize:Not today Cluckers")) {
			case 1:
				mes "[Cluckers]";
				mes "CLUCK! ^FF0000~Sure thing!~^000000";
				emotion 33;
				close2;
				announce "[Cluck! Cluck! Boom!] is about to start in Prontera!",bc_blue;
				initnpctimer;
				end;
			case 2:
				mes "[Cluckers]";
				mes "Cluck, cluck cluck... Cluck! ^FF0000~The current prize is^000000 ^008000"+ $cluck_item_amount +" "+ getitemname($cluck_item_id) +".^000000";
				next;
				break;
			case 3:
				mes "[Cluckers]";
				mes "Cluck cluck cluck...";
				close;
		}
	}
	mes "Cluck cluck! Cluuuuuck?";
	mes "Cluck....";
	close;

OnTimer10000:
	announce "Please hurry below the fountain if you want to retrive the item from the crazy chicken!",bc_blue;
	end;
OnTimer30000:
	announce "Try to squeeze out the Ather from the chicken, if you're lucky you can have it as a reward.",bc_blue;
	end;
	stopnpctimer;
	set .startcluck,1;
	set $cluck_item_id,7000;	// Reward item ID
	set $cluck_item_amount,1;	// Reward item amount
	end;
}
